# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# oh-my-zsh plugin support
export ZSH=$(antibody home)/https-COLON--SLASH--SLASH-github.com-SLASH-robbyrussell-SLASH-oh-my-zsh

# your project folder that we can `c [tab]` to
export PROJECTS=~/Projects

# Default WPDC composer
export WPDC_COMPOSER=servers

# Fetch NPM_TOKEN from Keychain
export NPM_TOKEN=$(security find-generic-password -w -l NPM_TOKEN)

# FZF default command
export FZF_DEFAULT_COMMAND='ag -g ""'

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

bindkey -e

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# pure-prompt
# https://github.com/sindresorhus/pure
# npm install -g pure-prompt
autoload -Uz promptinit; promptinit
prompt pure

export PATH=$HOME/Library/Python/3.6/bin:$PATH

# thefuck?
eval $(thefuck --alias)

# antibody
source $HOME/.dotfiles/zsh/zsh_plugins.sh
